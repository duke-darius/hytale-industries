plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

ext {
    hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

// Adds the Hytale server as a build dependency, allowing you to reference and
// compile against their code. This requires you to have Hytale installed using
// the official launcher for now.
dependencies {
    implementation(files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar"))
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Updates the manifest.json file with the latest properties defined in the
// build.properties file. Currently we update the version and if packs are
// included with the plugin.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

// sourcesJar reads from src/main/resources directly, so ensure variants are generated first.
tasks.named('sourcesJar') {
}

// Creates a run configuration in IDEA that will run the Hytale server with
// your plugin and the default assets.
idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --assets=$hytaleHome/install/$patchline/package/game/latest/Assets.zip"
        if (includes_pack.toBoolean()) {
            programParameters += " --mods=${sourceSets.main.java.srcDirs.first().parentFile.absolutePath}"
        }
        if (load_user_mods.toBoolean()) {
            programParameters += " --mods=$hytaleHome/UserData/Mods"
        }
        workingDirectory = serverRunDir.absolutePath
    }
}
// === Version bump helpers ===
def propsFile = file('gradle.properties')
def manifestFile = file('src/main/resources/manifest.json')

def readVersion = {
    def p = new Properties()
    propsFile.withInputStream { p.load(it) }
    return p.getProperty('version')
}

def writeVersion = { newVersion ->
    def p = new Properties()
    propsFile.withInputStream { p.load(it) }
    p.setProperty('version', newVersion)
    propsFile.withOutputStream { os -> p.store(os, null) }

    if (!manifestFile.exists()) {
        throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
    }
    def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
    manifestJson.Version = newVersion
    manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
}

def bumpVersion = { Map opts ->
    def current = readVersion()
    def parts = current.tokenize('.').collect { it as int }
    if (parts.size() != 3) {
        throw new GradleException("Expected semantic version (major.minor.patch), got '${current}'")
    }
    if (opts.type == 'minor') {
        parts[1] = parts[1] + 1
        parts[2] = 0
    } else if (opts.type == 'patch') {
        parts[2] = parts[2] + 1
    } else {
        throw new GradleException("Unknown bump type: ${opts.type}")
    }
    def newVersion = parts.join('.')
    writeVersion(newVersion)
    logger.lifecycle("Version updated: ${current} -> ${newVersion}")
}

tasks.register('incrementBuildVer') {
    group = 'versioning'
    description = 'Increment patch/build version in gradle.properties and manifest.json'
    doLast { bumpVersion(type: 'patch') }
}

tasks.register('incrementMinorVer') {
    group = 'versioning'
    description = 'Increment minor version (resetting patch) in gradle.properties and manifest.json'
    doLast { bumpVersion(type: 'minor') }
}
